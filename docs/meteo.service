@doc     "Provides weather forecast information for Switzerland."
@author  "Patrice Neff"
@version "1.0"
service meteo;

external:webpy "meteo_data.py";

@doc       "Returns all available forecasts for a ZIP code in Switzerland."
@param zip "ZIP code for which to get weather forecast"
GetForecasts: GET "/{zip}" {
    validate zip, /[0-9]{4}/;
    log.info("Getting weather data for {zip}");
    string date = meteo_data.getEtagTimestamp();
    etag "{zip}_{date}";
    expires 2 hours;
    
    records cached<1 day> weather = meteo_data.getWeather(zip);
    
    @doc "Returns the weather forecast in XML format.
          Example:
          
          \\code
            <meteo>
                <zip>9000</zip>
                <weather>
                    <date>2008-06-07</date>
                    <temparature>18</temparature>
                    <type>17</type>
                    <zip>9000</zip>
                </weather>
                <weather>...</weather>
            </meteo>
          \\endcode"
    output<xml> {
        <meteo>
            <zip>{zip}</zip>
            {weather}
        </meteo>
    }
    
    output<csv> cached<1 day> {
        {weather}
    }
}

PUT "/{zip}" <- (date, type, temparature) {
    validate date, /[0-9]{4}-(01|02|03|04|05|06|07|08|09|10|11|12)-([0-2][0-9]|3(0|1))/;
    
}
